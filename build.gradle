import org.gradle.api.artifacts.maven.MavenDeployment

configure(allprojects) {
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'signing'

    version = '2.3-SNAPSHOT'
    group = "net.davidbuccola.${rootProject.name}"

    // Symbolic versions for select dependencies. The version of most dependencies is specified inline with the
    // dependency declaration. However, for certain artifact groups that have multiple artifacts or that show up often
    // the version is specified symbolically here to simplify maintenance.

    ext {
        gradleVersion = '1.4'
        jacksonVersion = '1.9.2'
        jerseyVersion = '1.12'
        junitVersion = '4.8.2'
        logbackVersion = '1.0.7'
        slf4jVersion = '1.6.6'
        springVersion = '3.0.5.RELEASE'
    }

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
}

configure(rootProject) {
    ext.gitHash = 'git log -1 --format=%H'.execute().text.trim()

    idea {
        project {
            ipr {
                withXml { provider ->
                    provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'

                    def gradleSettings = getFreshIprComponent(provider.node, 'GradleSettings')
                    gradleSettings.appendNode('option', [name: 'gradleHome', value: gradle.gradleHomeDir.absolutePath])

                    def javacSettings = getFreshIprComponent(provider.node, 'JavacSettings')
                    javacSettings.appendNode('option', [name: 'MAXIMUM_HEAP_SIZE', value: "256"])
                }
            }
        }
    }
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'eclipse'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'


    ext.gitHash = rootProject.gitHash  // So we don't execute git command multiple times

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("${project.buildDir}/classes/main")
            testOutputDir = file("${project.buildDir}/classes/test")
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava.srcDirs
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    tasks.withType(Compile) {
        options.debug = true
        options.encoding = 'UTF-8'
        options.compilerArgs = ['-Xlint:-options'] // Ignore warning about bootclasspath and source compatibility (for now)
    }

    tasks.withType(Jar) {
        manifest {
            attributes('Source-Revision': gitHash)
        }
    }

    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors() / 2
        jvmArgs '-Xmx512m', '-XX:MaxPermSize=256m', '-XX:+HeapDumpOnOutOfMemoryError'
    }

    signing {
        required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask('uploadArchives') }
        sign configurations.archives
    }

    install {
        repositories {
            mavenInstaller {
                pom.whenConfigured { generatedPom -> customizePom(generatedPom, project) }
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.whenConfigured { generatedPom ->
                    customizePom(generatedPom, project)
                }
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    if (project.hasProperty('sonatypeStagingUsername')) {
                        authentication(userName: sonatypeStagingUsername, password: sonatypeStagingPassword)
                    }
                }
                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    if (project.hasProperty('sonatypeStagingUsername')) {
                        authentication(userName: sonatypeStagingUsername, password: sonatypeStagingPassword)
                    }
                }
            }
        }
    }

    if (file("src/integrationTest").exists()) {
        sourceSets {
            integrationTest {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
            }
        }

        configurations {
            integrationTestCompile.extendsFrom testCompile
            integrationTestRuntime.extendsFrom testRuntime
        }

        task integrationTest(type: Test) {
            classpath = sourceSets.integrationTest.runtimeClasspath
            testClassesDir = sourceSets.integrationTest.output.classesDir
            testResultsDir = file("${project.testResultsDir}/$name")
        }

        idea {
            module {
                testSourceDirs += sourceSets.integrationTest.java.srcDirs
                testSourceDirs += sourceSets.integrationTest.resources.srcDirs
                scopes.TEST.plus.add(configurations.integrationTestCompile)
                scopes.TEST.plus.add(configurations.integrationTestRuntime)
            }
        }

        eclipse {
            classpath {
                plusConfigurations.add(configurations.integrationTestCompile)
                plusConfigurations.add(configurations.integrationTestRuntime)
            }
        }
    }


    checkstyle {
        sourceSets = [sourceSets.main]
        configFile = file("$rootProject.projectDir/config/checkstyle/checkstyle.xml")
        toolVersion = 5.6
    }

    findbugs {
        sourceSets = [sourceSets.main]
        excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
    }

    // Support for 'provided' dependencies in generated poms
    ext.providedDependencies = []
    ext.provided = { providedDependencies << it }
}

/**
 * Customize the auto-generated pom with additional items necessary for maven central publication.
 */
def customizePom(pom, gradleProject) {
    pom.project {
        name = gradleProject.name
        description = gradleProject.description
        url = "https://github.com/davidbuccola/${rootProject.name}"
        packaging 'jar'
        organization {
            name = 'David Buccola'
            url = 'https://github.com/davidbuccola'
        }
        developers {
            developer {
                id = 'davidbuccola'
                name = 'David Buccola'
                email = 'david.buccola@gmail.com'
            }
        }
        scm {
            url = "https://github.com/davidbuccola/${rootProject.name}"
            connection = "scm:git:git://github.com/davidbuccola/${rootProject.name}.git"
            developerConnection = "scm:git:git://github.com/davidbuccola/${rootProject.name}.git"
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        // Respect 'provided' dependencies
        gradleProject.providedDependencies.each { dep ->
            pom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
        }
    }
}

/**
 * Generates a new gradle wrapper. You'll want to do this if you change gradle version or if your jvm options change
 * below.
 */
task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'

    gradleVersion = project.gradleVersion

    // Establish the default memory settings for the gradle wrappers. These settings can still be overridden in the
    // environment with GRADLE_OPTS or JAVA_OPTS if desired. Note that you should avoid use of -Xms here because it has
    // a tendency to not play well with other settings in the environment if they exist.
    doLast() {
        def defaultJvmOpts = "-XX:MaxPermSize=128m -Xmx512m"
        File wrapperFile = file('gradlew')
        wrapperFile.text = wrapperFile.text.replace("DEFAULT_JVM_OPTS=\"\"", "DEFAULT_JVM_OPTS=\"$defaultJvmOpts\"")
        File wrapperBatFile = file('gradlew.bat')
        wrapperBatFile.text = wrapperBatFile.text.replace("set DEFAULT_JVM_OPTS=", "set DEFAULT_JVM_OPTS=$defaultJvmOpts")
    }
}

/**
 * Returns a brand new Ipr component node with no children. If an existing component node is found it is removed along
 * with all of its children and a fresh one is created.
 */
def getFreshIprComponent(Node projectNode, String componentName) {
    Node existingComponent = (Node) projectNode.component.find { it.@name == componentName }
    if (existingComponent) {
        projectNode.remove(existingComponent)
    }
    return projectNode.appendNode('component', [name: componentName])
}

